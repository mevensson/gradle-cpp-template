plugins {
	id 'de.undercouch.download' version '2.1.0'
}
import de.undercouch.gradle.tasks.download.Download

task wrapper(type: Wrapper) {
   gradleVersion = '2.9'
}

task downloadGoogleTest(type: Download) {
	src 'https://github.com/google/googletest/archive/release-1.7.0.zip'
	dest file('build/3pp/googletest-1.7.0.zip')
}

task unzipGoogleTest(dependsOn: downloadGoogleTest, type: Copy) {
	from zipTree(downloadGoogleTest.dest)
	into file('build/3pp')
}

apply plugin: 'cpp'
apply plugin: 'google-test'

model {
	binaries {
		withType(GoogleTestTestSuiteBinarySpec) {
			lib library: 'googleTest', linkage: 'static'

			if (targetPlatform.operatingSystem.linux) {
				cppCompiler.define "TEST"
				linker.args '-pthread'
			}
		}
		withType(SharedLibraryBinarySpec) {
			buildable = false
		}
		all {
			println "name=${name} buildable=${buildable}"
		}
	}

	components {
		googleTest(NativeLibrarySpec) {
			sources {
				cpp {
					builtBy tasks.unzipGoogleTest
					source {
						srcDir 'build/3pp/googletest-release-1.7.0'
						include 'src/gtest-all.cc'
						srcDir 'src/googleTest/cpp'
						include 'main.cc'
					}
					exportedHeaders {
						srcDir 'build/3pp/googletest-release-1.7.0'
						srcDir 'build/3pp/googletest-release-1.7.0/include'
					}
				}
			}
		}
		hello(NativeLibrarySpec)
		main(NativeExecutableSpec) {
			binaries.all {
				lib library: 'hello', linkage: 'static'
			}
		}
	}

	testSuites {
		googleTestTest {
			binaries.all {
				buildable = false
			}
		}
		helloTest
		mainTest {
			binaries.all {
				buildable = false
			}
		}
	}
}
